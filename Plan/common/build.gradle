dependencies {
    api "net.playeranalytics:platform-abstraction-layer-api:$palVersion"
    api project(":api")
    api project(":extensions")
    api "org.apache.commons:commons-text:$commonsTextVersion"
    api "org.apache.commons:commons-compress:$commonsCompressVersion"
    api "com.github.ben-manes.caffeine:caffeine:$caffeineVersion"
    api "org.slf4j:slf4j-api:$slf4jVersion"
    api "com.maxmind.geoip2:geoip2:$geoIpVersion"
    api "com.google.code.gson:gson:$gsonVersion"
    api "com.zaxxer:HikariCP:$hikariVersion"

    compileOnly "com.h2database:h2:$h2Version"
    compileOnly "mysql:mysql-connector-java:$mysqlVersion"
    compileOnly "org.xerial:sqlite-jdbc:$sqliteVersion"

    runtimeOnly "org.slf4j:slf4j-nop:$slf4jVersion"

    testImplementation project(":api")
    testImplementation "com.google.code.gson:gson:$gsonVersion"
    testImplementation "com.h2database:h2:$h2Version"
    testImplementation "org.xerial:sqlite-jdbc:$sqliteVersion"
}

import org.apache.tools.ant.filters.ReplaceTokens
task updateVersion(type: Copy) {
    from('src/main/resources') {
        include 'plugin.yml'
        include 'bungee.yml'
        include 'nukkit.yml'
    }
    into 'build/sources/resources/'
    filter(ReplaceTokens, tokens: [version: '' + project.ext.fullVersion])
}
processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    dependsOn updateVersion
    from 'build/sources/resources'
}

shadowJar {
    //dependsOn processResources

    // Exclude these files
    exclude "**/*.svg"
    exclude "**/*.psd"

    exclude "**/module-info.class"
    exclude "module-info.class"
    exclude 'META-INF/versions/' // Causes Sponge to crash
    exclude 'org/apache/http/**/*' // Unnecessary http client depended on by geolite2 implementation
    exclude 'mozilla/**/*'

    relocate 'com.maxmind', 'plan.com.maxmind'
    relocate 'com.fasterxml', 'plan.com.fasterxml'
    relocate 'com.zaxxer', 'plan.com.zaxxer'
    relocate 'com.google.protobuf', 'plan.com.google.protobuf'
    relocate 'com.google.gson', 'plan.com.google.gson'
    relocate 'com.google.errorprone', 'plan.com.google.errorprone'
    relocate 'org.h2', 'plan.org.h2'
    relocate 'org.bstats', 'plan.org.bstats'
    relocate 'org.slf4j', 'plan.org.slf4j'

    // Exclude test dependencies
    exclude "org/junit/**/*"
    exclude "org/opentest4j/**/*"
    exclude "org/checkerframework/**/*"
    exclude "org/apiguardian/**/*"
    exclude "org/mockito/**/*"
    exclude "org/selenium/**/*"
    exclude "org/jayway/**/*"
    exclude "google/protobuf/**/*"
    exclude "jargs/gnu/**/*"
}
